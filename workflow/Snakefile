# workflow/Snakefile
import os

configfile: "config.yaml"

SAMPLES = list(config["samples"].keys())
OUTDIR = config["output"]["dir"]

include: "rules/common.smk"
include: "rules/qc_fastp.smk"
include: "rules/align_star.smk"
include: "rules/bam_post.smk"
include: "rules/signal_5prime.smk"
include: "rules/tracks.smk"

FINAL_5P_BW_BLACKLIST = expand(
    f"{OUTDIR}/bigwig/{{sample}}/{{sample}}.R2firstbaseCPM.blacklist.bw",
    sample=SAMPLES
)

rule all:
    input:
        # QC
        expand(f"{OUTDIR}/qc/fastp/{{sample}}/merged_fastp_final.html", sample=SAMPLES),
        expand(f"{OUTDIR}/qc/fastp/{{sample}}/merged_fastp_final.json", sample=SAMPLES),
        f"{OUTDIR}/qc/multiqc/multiqc_report.html",

        # Alignment (final BAM kept)
        expand(f"{OUTDIR}/star/{{sample}}/{{sample}}.unique.mapq11.sorted.bam", sample=SAMPLES),
        expand(f"{OUTDIR}/star/{{sample}}/{{sample}}.unique.mapq11.sorted.bam.bai", sample=SAMPLES),

        # BigWig from BAM (coverage, CPM), with/without blacklist
        expand(f"{OUTDIR}/bigwig/{{sample}}/{{sample}}.bamCPM.noblacklist.bw", sample=SAMPLES),
        expand(f"{OUTDIR}/bigwig/{{sample}}/{{sample}}.bamCPM.blacklist.bw", sample=SAMPLES),

        # 5' end signal BigWig (Read2 first base, 1bp), with/without blacklist
        expand(f"{OUTDIR}/bigwig/{{sample}}/{{sample}}.R2firstbaseCPM.noblacklist.bw", sample=SAMPLES),
        FINAL_5P_BW_BLACKLIST,

        # Track files
        expand(f"{OUTDIR}/tracks/{{sample}}/{{sample}}.tracks.txt", sample=SAMPLES),

        # Cleanup sentinel
        f"{OUTDIR}/.tmp_cleanup.done"


rule cleanup_tmp:
    input:
        FINAL_5P_BW_BLACKLIST
    output:
        touch(f"{OUTDIR}/.tmp_cleanup.done")
    shell:
        r"""
        set -euo pipefail
        echo "[cleanup] Removing temporary directory: {OUTDIR}/tmp"
        rm -rf {OUTDIR}/tmp
        """
